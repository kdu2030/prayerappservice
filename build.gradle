plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.3'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'org.hidetake.swagger.generator' version '2.18.2'
}

group = 'com.kevin'
version = '0.0.1-SNAPSHOT'

swaggerSources {
	prayerAppService {
		def apiYamlPath = "${rootDir}/src/main/resources/api/openapi.yml"
		def configJsonPath = "${rootDir}/src/main/resources/api/config.json"
		inputFile = file(apiYamlPath)
		def ignoreFilePath = "${rootDir}/src/main/resources/api/.openapi-generator-ignore"

		code {
			language = "spring"
			configFile = file(configJsonPath)
			rawOptions = ["--ignore-file-override", ignoreFilePath] as List<String>
			components = [models: true, apis: true, supportingFiles: "ApiUtil.java"]
		}
	}
}

compileJava.dependsOn swaggerSources.prayerAppService.code
processResources.dependsOn swaggerSources.prayerAppService.code
sourceSets.main.java.srcDir "${swaggerSources.prayerAppService.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${swaggerSources.prayerAppService.code.outputDir}/src/main/resources"

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	// Database libraries
	runtimeOnly 'org.postgresql:postgresql'
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-database-postgresql'

	// Jwt Processing
	implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'

	// Swagger
	implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
	implementation 'io.swagger.core.v3:swagger-annotations:2.2.15'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'
	swaggerCodegen 'org.openapitools:openapi-generator-cli:7.8.0'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// Test libraries
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

}

tasks.named('test') {
	useJUnitPlatform()
}
